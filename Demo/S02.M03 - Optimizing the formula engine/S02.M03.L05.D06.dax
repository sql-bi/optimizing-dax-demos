--
--  Database: Contoso 100M.PBIX (DQ Tables)
--  CPU:      AMD Ryzen Threadripper 3970X 32-Core Processor 3.72 GHz
--
DEFINE
    MEASURE 'Sales DQ'[# Customers] =
        DISTINCTCOUNT ( 'Sales DQ'[CustomerKey] )
    MEASURE 'Sales DQ'[Growth] =
        VAR Vals =
            ADDCOLUMNS ( ALLSELECTED ( 'Date DQ'[Year] ), "@CY", [# Customers] )
        VAR ValsPrevYear =
            SELECTCOLUMNS ( Vals, "@Year", 'Date DQ'[Year] + 1, "@PY", [@CY] )
        VAR ValsCurYear =
            SELECTCOLUMNS ( Vals, "@Year", 'Date DQ'[Year] + 0, "@CY", [@CY] )
        VAR AllVals =
            NATURALINNERJOIN ( ValsCurYear, ValsPrevYear )
        VAR CurrentYear =
            SELECTEDVALUE ( 'Date DQ'[Year] )
        VAR Result =
            MINX (
                FILTER ( AllVals, [@Year] = CurrentYear ),
                IF ( [@CY] > 0 && [@PY] > 0, DIVIDE ( [@CY] - [@PY], [@PY] ) )
            )
        RETURN
            Result

EVALUATE
SUMMARIZECOLUMNS ( 'Date DQ'[Year], "Growth %", [Growth] )
