--
--  Database: Contoso 100M.PBIX
--  CPU:      AMD Ryzen Threadripper 3970X 32-Core Processor 3.72 GHz
--
DEFINE
    MEASURE Sales[Avg Var] =
        VAR Stores = 
            VALUES ( 'Store'[Name] )
        VAR StoresAndFirstLastDate =
            ADDCOLUMNS (
                Stores,
                "First Sale", CALCULATE ( MIN ( Sales[Order Date] ) ),
                "Last Sale", CALCULATE ( MAX ( Sales[Order Date] ) )
            )
        VAR StoresAndFirstDate =
            TREATAS (
                SELECTCOLUMNS (
                    StoresAndFirstLastDate,
                    Store[Name],
                    [First Sale]
                ),
                Store[Name],
                Sales[Order Date]
            )
        VAR StoresAndLastDate =
            TREATAS (
                SELECTCOLUMNS (
                    StoresAndFirstLastDate,
                    Store[Name],
                    [Last Sale]
                ),
                Store[Name],
                Sales[Order Date]
            )
        VAR Result =
            AVERAGEX (
                Stores,
                VAR StartPrice =
                    CALCULATE ( AVERAGE ( Sales[Net Price] ), KEEPFILTERS ( StoresAndFirstDate ) )
                VAR EndPrice =
                    CALCULATE ( AVERAGE ( Sales[Net Price] ), KEEPFILTERS ( StoresAndLastDate ) )
                RETURN
                    ( EndPrice - StartPrice ) / StartPrice
            )
        RETURN
            Result

EVALUATE
ADDCOLUMNS ( VALUES ( 'Product'[Product Name] ), "Avg Var%", [Avg Var] )
ORDER BY [Product Name]
