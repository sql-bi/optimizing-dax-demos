--
--  Database: Contoso 100M.PBIX
--  CPU:      AMD Ryzen Threadripper 3970X 32-Core Processor 3.72 GHz
--
DEFINE
    MEASURE Sales[AmtLocalCurrency] =
        VAR AggregatedSales =
            ADDCOLUMNS (
                SUMMARIZE (
                    Sales,
                    Sales[Order Date],
                    Sales[Currency Code]
                ),
                "@Sales Amount", [Sales Amount]
            )
        VAR ExchangeRates =
            CALCULATETABLE (
                TREATAS (
                    SUMMARIZE (
                        'CurrencyExchange',
                        'CurrencyExchange'[Exchange],
                        'CurrencyExchange'[ToCurrency],
                        'CurrencyExchange'[Date]
                    ),
                    'CurrencyExchange'[Exchange],
                    Sales[Currency Code],
                    Sales[Order Date]
                ),
                'CurrencyExchange'[FromCurrency] = "USD"
            )
        VAR Conversion =
            NATURALINNERJOIN (
                AggregatedSales,
                ExchangeRates
            )
        VAR Result =
            SUMX (
                Conversion,
                [@Sales Amount] * 'CurrencyExchange'[Exchange]
            )
        RETURN
            Result

EVALUATE 
SUMMARIZECOLUMNS (
    Customer[Country],
    Sales[Currency Code],
    "Amt Local", [AmtLocalCurrency]
)
ORDER BY
    Customer[Country],
    Sales[Currency Code]

